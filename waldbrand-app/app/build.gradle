import de.topobyte.BuildUtil

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 33
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId 'de.waldbrandapp.brandenburg'
        minSdkVersion 14
        targetSdkVersion 33
        versionCode 25
        versionName "2.15.0"
        buildConfigField "int", "MAP_FILE_SIZE", "" +
                new File("app/src/main/assets/map.xmap.jet").size()
        buildConfigField "int", "DATABASE_FILE_SIZE", "" +
                BuildUtil.unGzippedSize(new File("app/src/main/assets/map.sqlite.jet"))
        buildConfigField "String", "DATABASE_FILE_MD5", "\"" +
                BuildUtil.md5(new File("app/src/main/assets/map.sqlite.jet")) + "\""
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'darwin/x86_64/liblz4-java.dylib'
        exclude 'win32/amd64/liblz4-java.so'
        exclude 'linux/amd64/liblz4-java.so'
        exclude 'linux/i386/liblz4-java.so'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

configurations {
    implementation.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    implementation.exclude group: 'log4j', module: 'log4j'
    implementation.exclude group: 'com.google.android', module: 'android'
}

dependencies {
    implementation 'de.topobyte:mapocado-android:0.3.0'
    implementation 'de.topobyte:nomioc-luqe:0.1.0'
    implementation 'de.topobyte:luqe-android:0.2.1'
    implementation 'de.topobyte:android-seekbarpreference:0.0.1'
    implementation 'de.topobyte:adt-multicollections:0.0.4'
    implementation 'de.topobyte:android-app-version-util:0.0.1'
    implementation 'de.topobyte:android-map-zoom-controls:0.0.2'
    implementation 'de.topobyte:android-fullscreen:0.0.1'
    implementation 'de.topobyte:sqlite-spatial:0.2.0'
    implementation 'de.topobyte:geomath:0.1.0'
    implementation 'de.topobyte:melon-enums:0.0.1'
    implementation 'de.topobyte:android-map-utils:0.2.0'
    implementation 'de.topobyte:android-intent-utils:0.1.0'
    implementation 'com.slimjars.trove4j:trove4j-int-int-hash-map:1.0.1'
    implementation project(':loader-util')
    implementation project(':common-resources')
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'org.slf4j:slf4j-android:1.7.12'
    implementation 'pub.devrel:easypermissions:1.3.0'
    implementation 'org.locationtech.proj4j:proj4j:1.1.3'

    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'de.topobyte:luqe-jdbc:0.2.1'
    testImplementation 'org.slf4j:slf4j-api:1.7.12'
    testImplementation 'org.xerial:sqlite-jdbc:3.8.9.1'
}
